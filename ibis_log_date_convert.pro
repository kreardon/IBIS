;-------------------------------------------------------------
;+
; NAME:
;       ibis_log_date_convert
; PURPOSE:
;       Convert date text from IBIS log files into Julian date
; CATEGORY:
; CALLING SEQUENCE:
;       julian_date = ibis_log_date_convert(input_date)
; INPUTS:
;       input_date = DD/Mon/YYYY HH24:MI:SS
; KEYWORD PARAMETERS:    
;       fits_date_obs = returns date in FITS DATE-OBS format
; OUTPUTS:
;       julian_date = DBLARR(1), generated by JULDAY
; COMMON BLOCKS:
;       
; NOTES:
;       The input date format is: DD/Mon/YYYY HH24:MI:SS
;       Example: 14/Apr/2003 13:51:27
;       Leading spaces, if any, are removed
; MODIFICATION HISTORY:
;       19 Jul, 2003 - KPR - added documentation
;       24 Sep, 2009 - KPR - added fits_date_obs keyword
;-
;-------------------------------------------------------------

FUNCTION ibis_log_date_convert, input_date, fits_date_obs=fits_date_obs

month_names          = ['', 'Jan', 'Feb', 'Mar', 'Apr', 'May', 'Jun', 'Jul', 'Aug', 'Sep', 'Oct', 'Nov', 'Dec']
date_separator       = '/'
fits_date_separator  = '-'
time_separator       = ':'

; remove any leading (or trailing spaces) so that date text always starts at 
; beginning of string (hence string_offset is set to 0)
input_date           = STRTRIM(input_date, 2)
string_offset        = 0

input_year           = FIX(STRMID(input_date,    7 + string_offset, 4))
input_month          =     STRMID(input_date,    3 + string_offset, 3)
month_number         = WHERE(STRMATCH(month_names, input_month, /FOLD_CASE))
input_day            = FIX(STRMID(input_date,    0 + string_offset, 2))
input_hour           = FIX(STRMID(input_date,   12 + string_offset, 2))
input_min            = FIX(STRMID(input_date,   15 + string_offset, 2))
input_sec            = FLOAT(STRMID(input_date, 18 + string_offset))

;constructed_date    =  construct_day + date_separator + construct_month_name + date_separator + $
;                            construct_year + ' ' + construct_hour + time_separator + construct_min

fits_date_obs        =  STRING(input_year,FORMAT='(I4.4)')   + fits_date_separator + $
                        STRING(month_number,FORMAT='(I2.2)') + fits_date_separator + $
                        STRING(input_day,FORMAT='(I2.2)')    + 'T' + $
                        STRING(input_hour,FORMAT='(I2.2)')   + time_separator + $
                        STRING(input_min,FORMAT='(I2.2)')    + time_separator + $
                        STRING(input_sec,FORMAT='(F06.3)')

julian_date          = JULDAY(month_number, input_day, input_year, input_hour, input_min, input_sec)

RETURN, julian_date

END                  ; END ibis_log_date_convert

